
#Область ПрограммныйИнтерфейс

// Возвращает структуру массива видов схем интеграции.
// 
// Возвращаемое значение:
//  Массив из структура - структура массива видов схем интеграции 
//
Функция ПолучитьВидыСхемИнтеграции() Экспорт

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Axapta_ВидыСхемИнтеграции.Ссылка КАК Ссылка,
	|	Axapta_ВидыСхемИнтеграции.Код КАК code,
	|	Axapta_ВидыСхемИнтеграции.Наименование КАК name
	|ИЗ
	|	Справочник.Axapta_ВидыСхемИнтеграции КАК Axapta_ВидыСхемИнтеграции
	|ГДЕ
	|	Axapta_ВидыСхемИнтеграции.ПометкаУдаления = ЛОЖЬ
	|");
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивВидовСхем = Новый Массив();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураЭлемента = Новый Структура("code, name, schemes", );
		ЗаполнитьЗначенияСвойств(СтруктураЭлемента, Выборка);
		
		МассивСхем = Новый Массив;
		
		Для Каждого СтрокаОбработчика Из Выборка.Ссылка.Обработчики Цикл
			
			ОписаниеОбработчика = ПолучитьОписаниеОбработчикаHTTP(СтрокаОбработчика.ИмяОбработчика);
			МассивСхем.Добавить(ОписаниеОбработчика);
			
		КонецЦикла;	
		
		СтруктураЭлемента.schemes = МассивСхем;
		МассивВидовСхем.Добавить(СтруктураЭлемента);
		
	КонецЦикла;	
	
	СтруктураУровня_typesSchemes = Новый Структура("typesSchemes", МассивВидовСхем);
	
	Возврат СтруктураУровня_typesSchemes;
	
КонецФункции

// Возвращает строку JSON из массива.
// 
// Параметры:
//  МассивДанных - Массив.
// 
// Возвращаемое значение:
//  Строка - cтрока в формате JSON из массива данных
//
Функция ПолучитьСтрокуJSONИзМассива(МассивДанных) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, МассивДанных);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

// Получить обработчики видов схем.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица значений(ИмяОбработчика, СинонимОбработчика) 
//
Функция ПолучитьОбработчикиВидовСхем() Экспорт
	
	Обработчики = Новый ТаблицаЗначений();
	Обработчики.Колонки.Добавить("ИмяОбработчика");
	Обработчики.Колонки.Добавить("СинонимОбработчика");
	
	Для Каждого Обработчик Из Метаданные.Обработки Цикл
		
		Если СтрЧислоВхождений(Обработчик.Имя, "Axapta_Обработчик") = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		НоваяСтрока = Обработчики.Добавить();
		НоваяСтрока.ИмяОбработчика = Обработчик.Имя;
		НоваяСтрока.СинонимОбработчика = Обработчик.Синоним;

	КонецЦикла;	
	
	Возврат Обработчики;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


// Получить описание обработчика HTTP.
// 
// Параметры:
//  ИмяОбработчика - Строка - Имя обработчика
// 
// Возвращаемое значение:
//  Структура -  Получить описание обработчика HTTP:
// * name - Строка 
// * synonym - Строка
// * comment - Строка
// * parameters - Массив из структура - массив из структуры параметра
//
Функция ПолучитьОписаниеОбработчикаHTTP(ИмяОбработчика) Экспорт
	
	Обработчик = Метаданные.Обработки[ИмяОбработчика];
	Параметры = Новый Массив;
	
	СтруктураСвойств = Новый Структура();
	СтруктураСвойств.Вставить("name", ИмяОбработчика);
	СтруктураСвойств.Вставить("synonym", Обработчик.Синоним);
	СтруктураСвойств.Вставить("comment", Обработчик.Комментарий);
	СтруктураСвойств.Вставить("parameters", Параметры);
	
	Для Индекс = 0 По Обработчик.Реквизиты.Количество() - 1 Цикл
		
		ТекущийПараметр = Обработчик.Реквизиты[Индекс];
		МассивТипов = ТекущийПараметр.Тип.Типы();
		МассивТиповСтрокой = Новый Массив;
		
		Для Каждого ТекущийТип Из МассивТипов Цикл
			
			// Метод не работает с объектами из расширений
			//ТипСтрокой = ОбщегоНазначения.СтроковоеПредставлениеТипа(ТекущийТип);
			
			ТипСтрокой = СтроковоеПредставлениеТипа(ТекущийТип);
			МассивТиповСтрокой.Добавить(ТипСтрокой);
			
		КонецЦикла;	

		СтруктураПараметра = Новый Структура();
		СтруктураПараметра.Вставить("name", ТекущийПараметр.Имя);	
		СтруктураПараметра.Вставить("synonym", ТекущийПараметр.Синоним);
		СтруктураПараметра.Вставить("types", МассивТиповСтрокой);									
		
		Параметры.Добавить(СтруктураПараметра);
		
	КонецЦикла;	
	
	Возврат СтруктураСвойств;
	
КонецФункции

// Возвращает строковое представление типа. 
// Для ссылочных типов возвращает в формате "СправочникСсылка.ИмяОбъекта" или "ДокументСсылка.ИмяОбъекта".
// Для остальных типов приводит тип к строке, например "Число".
//
// Параметры:
//  Тип - Тип - для которого надо получить представление.
//
// Возвращаемое значение:
//  Строка - представление типа
//
Функция СтроковоеПредставлениеТипа(Тип)

	Представление = "";

	Если Не Метаданные.НайтиПоТипу(Тип) = Неопределено Тогда

		ПолноеИмя = Метаданные.НайтиПоТипу(Тип).ПолноеИмя();
		РазделеннаяСтрока = СтрРазделить(ПолноеИмя, ".");

		ИмяОбъекта = РазделеннаяСтрока[1];
		ТипОбъекта = РазделеннаяСтрока[0];

		Если ТипОбъекта = "Справочник" Тогда
			Представление = "СправочникСсылка";

		ИначеЕсли ТипОбъекта = "Документ" Тогда
			Представление = "ДокументСсылка";

		ИначеЕсли ТипОбъекта = "БизнесПроцесс" Тогда
			Представление = "БизнесПроцессСсылка";

		ИначеЕсли ТипОбъекта = "ПланВидовХарактеристик" Тогда
			Представление = "ПланВидовХарактеристикСсылка";

		ИначеЕсли ТипОбъекта = "ПланСчетов" Тогда
			Представление = "ПланСчетовСсылка";

		ИначеЕсли ТипОбъекта = "ПланВидовРасчета" Тогда
			Представление = "ПланВидовРасчетаСсылка";

		ИначеЕсли ТипОбъекта = "Задача" Тогда
			Представление = "ЗадачаСсылка";

		ИначеЕсли ТипОбъекта = "ПланОбмена" Тогда
			Представление = "ПланОбменаСсылка";

		ИначеЕсли ТипОбъекта = "Перечисление" Тогда
			Представление = "ПеречислениеСсылка";

		КонецЕсли;

		Результат = ?(Представление = "", Строка(Тип), Представление + "." + ИмяОбъекта);

	Иначе
		Результат = Строка(Тип);

	КонецЕсли;

	УдалитьПрефиксРасширенияИзСтроки(Результат);

	Возврат Результат;

КонецФункции

Процедура УдалитьПрефиксРасширенияИзСтроки(ТекущаяСтрока) Экспорт
	
	ТекущаяСтрока = СтрЗаменить(ТекущаяСтрока, "Axapta_", "");	
	
КонецПроцедуры	

#КонецОбласти




